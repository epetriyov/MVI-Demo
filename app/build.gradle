apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion build_versions.compileSdk
    defaultConfig {
        applicationId "com.connect.android.client"
        minSdkVersion build_versions.minSdk
        targetSdkVersion build_versions.targetSdk
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "API_BASE_URL", "\"https://api.connectim.pro\""
        buildConfigField "String", "SOCKET_URL", "\"wss://wsapi.connectim.pro/\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    androidExtensions {
        experimental = true
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation deps.androidx.appcompat
    implementation deps.androidx.recyclerview
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.core_ktx

    implementation deps.androidx.room.runtime
    implementation deps.androidx.room.rxjava2
    kapt deps.androidx.room.compiler

    implementation deps.androidx.navigation.fragment
    implementation deps.androidx.navigation.ui

    implementation deps.gms.location
    implementation deps.gms.maps

    implementation deps.firebase.core
    implementation deps.firebase.messaging

    implementation deps.rx.java
    implementation deps.rx.kotlin
    implementation deps.rx.android

    implementation deps.rxbinding2.kotlin
    implementation deps.rxbinding2.design_kotlin

    implementation deps.retrofit.core
    implementation deps.retrofit.adapter_rxjava2
    implementation deps.retrofit.converter_moshi

    implementation deps.moshi.core
    implementation deps.moshi.adapters
    kapt deps.moshi.koltin_codegen

    implementation deps.glide.core
    kapt deps.glide.compiler

    implementation deps.koin.core
    implementation deps.koin.android
    implementation deps.koin.androidx_scope
    implementation deps.koin.androidx_viewmodel

    implementation deps.scarlet.core
    implementation deps.scarlet.websocket
    implementation deps.scarlet.moshi_adapter
    implementation deps.scarlet.rxjava2_adapter
    implementation deps.scarlet.android_lifecycle_adapter

    implementation deps.kotlin
    implementation deps.material
    implementation deps.crashlytics
    implementation deps.rxpermissions
    implementation deps.rxlocation
    implementation deps.jodatime
    implementation deps.rxredux
    implementation deps.timber
    implementation deps.vksdk
    implementation deps.facebooksdk
    implementation deps.card_stack_view
}
